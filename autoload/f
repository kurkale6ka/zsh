# Find files using a 'fuzzy' search

# Use fd...
if (( $+commands[fd] ))
then
   if (($#))
   then
      fd --strip-cwd-prefix -tf -up -E.git -E'*~' $@
   elif (( $+commands[fzf] ))
   then
      # I want -0 here, that's why I don't use FZF_DEFAULT_COMMAND
      fd --strip-cwd-prefix -tf -u -E.git -E'*~' -0 | fzf --read0 -0 -1
   fi
   return
fi

# ...or find
#
# a few limitations:
# - 0/1 arguments only allowed
# - heedless of .gitignore files
# - always ignore case
# - searches for hidden files (* matches .)

(($# <= 1)) || return 1

# use -path to ignore .folders: .git, .svn, ...
# TODO: fix as above, not ignoring stuff
if (($#))
then
   find . -xdev \( -path '*/\.*' -o -fstype sysfs -o -fstype devfs -o -fstype devtmpfs -o -fstype proc \) -prune -o -type f ! -name '*~' -iname "*$1*" -printf '%M %u %g %P\n'
elif (( $+commands[fzf] ))
then
   find . -xdev \( -path '*/\.*' -o -fstype sysfs -o -fstype devfs -o -fstype devtmpfs -o -fstype proc \) -prune -o -type f ! -name '*~' -printf '%P\0' | fzf --read0 -0 -1
fi
