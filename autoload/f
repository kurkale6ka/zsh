# Find files & folders using a 'fuzzy' search

# Use fd...
if (( $+commands[fd] ))
then
   if (($#))
   then
      fd --show-errors -E'*~' $@
   elif (( $+commands[fzf] ))
   then
      fd --show-errors -E'*~' -0 | fzf --read0 -0 -1
   fi
   return
fi

# ...or find
#
# a few limitations:
# - 0/1 arguments only allowed
# - heedless of .gitignore files
# - always ignore case
# - searches for hidden files (* matches .)

(($# <= 1)) || return 1

# ignore CVS folders
local -a cvs
cvs=(.git .svn .hg)

if (($#))
then
   find . -xdev \( -name ${(zj. -o -name .)cvs} \) -prune -o ! -name '*~' -iname "*$1*" -printf '%M %u %g %P\n'
elif (( $+commands[fzf] ))
then
   find . -xdev \( -name ${(zj. -o -name .)cvs} \) -prune -o ! -name '*~' -printf '%P\0' | fzf --read0 -0 -1
fi
