# Find files using a 'fuzzy' search

# Use fd...
if (( $+commands[fd] ))
then
   if (($#))
   then
      fd --show-errors -tf -E'*~' $@
   elif (( $+commands[fzf] ))
   then
      fd --show-errors -tf -E'*~' -0 | fzf --read0 -0 -1
   fi
   return
fi

# ...or find
#
# a few limitations:
# - 0/1 arguments only allowed
# - heedless of .gitignore files
# - always ignore case
# - searches for hidden files (* matches .)

(($# <= 1)) || return 1

# ignore CVS folders
local -a cvs
cvs=(.git .svn .hg)

if (($#))
then
   find . -xdev \( -name ${(zj. -o -name .)cvs} \) -prune -o -type f ! -name '*~' -iname "*$1*" -printf '%M %u %g %P\n'
elif (( $+commands[fzf] ))
then
   find . -xdev \( -name ${(zj. -o -name .)cvs} \) -prune -o -type f ! -name '*~' -printf '%P\0' | fzf --read0 -0 -1
fi
