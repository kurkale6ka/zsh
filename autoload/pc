# fuzzy search for pass passwords
#
# pass - the standard UNIX password manager
# https://www.passwordstore.org/

local to_stdout

local pstore
local pfile

pstore=~/.password-store

# options
if [[ ${@[(i)-*]} -le $# ]]
then
   local -a patterns
   while :
   do
      case $1 in
         (-h|--help)
            print -P 'Usage: pc [-%F{yellow}o%f] - copy password (or output to %F{yellow}STDOUT%f)'
            return
            ;;
         (-o)
            to_stdout=1
            shift
            ;;
         ([^-]*)
            patterns+=($1)
            shift
            ;;
         (-?*)
            print -P "Error: unknown option %F{red}$1%f" >&2
            return 1
            ;;
         (*)
            break
            ;;
      esac
   done
   if [[ $patterns ]]
   then
      set $patterns
   fi
fi

# get pass
if (($#))
then
   pfile=$(cd $pstore && fd -e gpg -E'*~' -0 | sed -z 's/\.gpg$//' | fzf -q"$*" --read0 -0 -1 --cycle)
else
   pfile=$(cd $pstore && fd -e gpg -E'*~' -0 | sed -z 's/\.gpg$//' | fzf --read0 -0 -1 --cycle)
fi

if [[ $pfile ]]
then
   if ((to_stdout))
   then
      # output
      print -P "%F{yellow}$pfile%f"
      gpg -q -d $pstore/$pfile.gpg
   else
      # TODO: port to Linux
      local clip_prev=$(pbpaste)

      # copy to clipboard
      print -P "Copying %F{yellow}$pfile%f to the clipboard..."
      gpg -q -d $pstore/$pfile.gpg | head -n1 | tr -d '\n' | xclip

      # keep password for 45sec, then reset clipboard to previous entry
      # TODO: test edge case when no previously copied entries
      if [[ $clip_prev && $pipestatus == '0 0 0 0' ]]
      then
         setopt local_options no_monitor
         { sleep 45; echo -n $clip_prev | xclip } & disown
      fi
   fi
fi
