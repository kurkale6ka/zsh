# cal wrapper aiming to standardise output and available options
#
# output:
#
# - always start weeks on Monday
# - always display days horizontally instead of vertically
#
# options:
#
# - make -3|y always available

# Linux
if [[ $(uname) == Linux ]]
then
   if (( $+commands[ncal] ))
   then
      ncal -Mb $@
   else
      command cal -m $@
   fi
   return
fi

# OpenBSD, ...
if [[ $(uname) != Darwin ]]
then
   # Non Linux version of cal, support for -m3y is unknown
   command cal $@
else
   # Mac OS
   if (( $+commands[gcal] ))
   then
      case $1 in
         (-3) gcal -s1 .   ${@:2} ;;
         (-y) gcal -s1 -b4 ${@:2} ;;
         ( *)
              if (($# != 0))
              then
                 echo 'Usage: cal -3|y [options...], switching to gcal' 1>&2
              fi
              gcal -s1 $@ ;;
      esac
   else
      # BSD version of cal, support for -m3y is unknown
      command cal $@
      print -P '%F{red}Please install gcal!%f'
   fi
fi
