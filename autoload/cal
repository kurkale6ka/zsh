# cal wrapper aiming to standardise output and available options
#
# output:
#
# - always start weeks on Monday
# - always display days horizontally instead of vertically
#
# options:
#
# - make -3|-y always available

local arg
local arg_cal
local arg_gcal

# Parse -3 and -y
for arg in $@
do
   shift
   if [[ $arg == -[3y] ]]
   then
      arg_cal=${arg#-}
      if [[ $arg == -3 ]]
      then
         arg_gcal=.
      else
         arg_gcal=-b4
      fi
      # keep parsing; skip the set assignment
      continue
   fi
   set -- $@ $arg
done

# Mac OS
if [[ $(uname) == Darwin ]]
then
   if (( $+commands[gcal] ))
   then
      gcal -s1 $arg_gcal $@
   else
      # BSD version of cal, support for -m3y is unknown
      command cal $@
      print -P '%F{red}Please install gcal!%f'
   fi
   return
fi

# OpenBSD, ...
# Non Linux version of cal, support for -m3y is unknown
if [[ $(uname) != Linux ]]
then
   command cal $@
   return
fi

# Linux
if (( $+commands[ncal] ))
then
   ncal -Mb$arg_cal $@
else
   command cal -m$arg_cal $@
fi
